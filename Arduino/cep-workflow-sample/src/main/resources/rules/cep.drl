//created on: 2017. 3. 26
package com.sample.cep

//list any import classes here.

import com.sample.cep.*;
import com.sample.process.*;

//declare any global variables here

declare ThermoSensorData
@role(event)
end

declare SmokeSensorData
@role(event)
end


rule "High Temperature Rule"
	no-loop true
	lock-on-active true
	//	Fire Alarm이 활성화 되지 않은 상태에서, 온도가 80도를 넘을 경우
    when
        fad: FanActuatorData(on == false) &&
        tsd: ThermoSensorData(temperature > 80.0, value: temperature) from entry-point thermometer
    then
        System.out.println("*** High Temperature Alert: " + value + " ***");

end

rule "Dense Smoke Rule"
	no-loop true
	lock-on-active true
	//	Fire Alarm이 활성화 되지 않은 상태에서, 연기 농도가 18을 넘을 경우
    when
        fad: FanActuatorData(on == false) &&
        ssd: SmokeSensorData(smokeLevel > 18.0, value: smokeLevel) from entry-point smoke 
    then
        System.out.println("*** Dense Smoke Alert: " + value + " ***");

end

rule "Fire Alarm Rule"
	no-loop true
	lock-on-active true
	//	Fire Alarm이 활성화 되지 않은 상태에서, 2초 사이에 한 번이라도 온도가 80도를 넘고 연기 농도가 18을 넘을 경우
    when
        fad: FanActuatorData(on == false) &&
        tsd: ThermoSensorData(temperature > 80.0, t: temperature) over window:time(2s) from entry-point thermometer &&
        ssd: SmokeSensorData(smokeLevel > 18.0, s: smokeLevel) over window:time(2s) from entry-point smoke 
    then
        System.out.println("*** Fire Alarm !!!!!!!!!!!!!!!!!!! ***");
        ProcessTest.startProcess();
		fad.setOn(true);
		update(fad);

end

rule "Fire Alarm Off Rule"
	no-loop true
	lock-on-active true
	//	Fire Alarm이 활성화 된 상태에서, 2초 동안 한 번도 온도가 40도를 넘지 않고 연기 농도가 18을 넘지 않을 경우
    when
        fad: FanActuatorData(on == true) &&
    	accumulate(
			ThermoSensorData(temperature >= 40.0) over window:time(2s) from entry-point thermometer; 
			$cnt1: count(1);
			$cnt1 == 0
	    ) &&
    	accumulate(
			SmokeSensorData(smokeLevel >= 10.0) over window:time(2s) from entry-point smoke;
			$cnt2: count(1);
			$cnt2 == 0
	    )
    then
        System.out.println("*** Fire Alarm Off ***");
		fad.setOn(false);
		update(fad);

end


